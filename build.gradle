import java.text.SimpleDateFormat

plugins {
    id 'java-library'
    id 'net.nemerosa.versioning' version '3.0.0'
}

normalization {
    runtimeClasspath{
        ignore('/META-INF/MANIFEST')
    }
}

repositories {
    mavenCentral()
}

Date buildTimeAndDate = new Date()
ext {
    buildDate = new SimpleDateFormat('yyyy-MM-dd').format(buildTimeAndDate)
    buildTime = new SimpleDateFormat('HH:mm:ss.SSSZ').format(buildTimeAndDate)
}

dependencies {
    implementation("com.google.code.gson:gson:2.10.1")
}

ext.moduleName = 'eu.hansolo.cvescanner'
description    = 'CVEScanner is a tool to get cves from NVD related to OpenJDK and GraalVM'

jar {
    from {
        duplicatesStrategy = DuplicatesStrategy.EXCLUDE
        configurations.runtimeClasspath.collect {  it.isDirectory() ? it : zipTree(it)  }
    } {
        exclude "META-INF/*.SF"
        exclude "META-INF/*.DSA"
        exclude "META-INF/*.RSA"
    }
    manifest {
        attributes(
                'Built-By'              : System.properties['user.name'],
                'Created-By'            : System.properties['java.version'] + " (" + System.properties['java.vendor'] + " " + System.properties['java.vm.version'] + ")",
                'Build-Date'            : project.buildDate,
                'Build-Time'            : project.buildTime,
                'Build-Revision'        : versioning.info.commit,
                'Specification-Title'   : project.name,
                'Specification-Version' : project.version,
                'Implementation-Title'  : project.name,
                'Implementation-Version': project.version,
                'Bundle-Name'           : project.name,
                'Bundle-License'        : 'https://www.apache.org/licenses/LICENSE-2.0;description=Apache License Version 2.0;link=https://spdx.org/licenses/Apache-2.0.html',
                'Bundle-Description'    : description,
                'Bundle-SymbolicName'   : 'eu.hansolo.cvescanner',
                'Class-Path'            : '${project.name}-${project.version}.jar'
        )
    }
}

// start the from gradle
task Main(type: JavaExec) {
    mainClass = "eu.hansolo.cvescanner.Main"
    classpath = sourceSets.main.runtimeClasspath
}
